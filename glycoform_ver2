# Glycoform version 2

## Parameters

* `id` Source UniProt ID
  * default: P01218
  * examples: P05556, P02765, P17301, P35222

## Endpoint

https://sparql.glyconavi.org/sparql/

## `result`


```sparql

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX glycan: <http://purl.jp/bio/12/glyco/glycan#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX gs: <http://glyconavi.org/glycosample/>
PREFIX ga: <http://glyconavi.org/glyabun/>
PREFIX exterms: <http://www.example.org/terms/>
PREFIX chebi: <http://bio2rdf.org/chebi:>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX up: <http://purl.uniprot.org/core/>

SELECT distinct ?pos ?order ?from ?to  ?wurcs ?glycan_image ?mod_wurcs ?mod_glycan_image ?glytoucan ?gsid ?uniprotid ?seq

FROM <http://glyconavi.org/database/glycoabun>
WHERE {

	VALUES ?uniprotid { "{{params.id}}" }
    
    
    ?gs glycan:has_resource_entry ?br .
    ?gs gs:glycosample_id ?gsid .
    ?br sio:has-component-part ?rc .
    ?rc sio:has-component-part ?pep .
    ?pep ga:aa_sequence ?seq .
    ?pep ga:uniprot_id ?uniprotid .

    ?rc ga:has_abundance ?ga .
    ?ga dcterms:identifier ?gaid .
    ?ga sio:has-direct-part ?gf .

    # abundance
    ?gf sio:has-proper-part ?gi .
    ?gf ga:order ?order .

    #glycosylation site
    OPTIONAL {
        ?gf sio:is-covalently-connected-to ?loc .
        ?loc faldo:begin ?begin .
        ?begin faldo:position ?pos .
    }

    # glycan structure
    ?gi sio:has-part ?gcomp .
    ?glycan chebi:32854 ?gcomp .
    ?glycan ga:wurcs ?wurcs .
    ?glycan foaf:depiction ?glycan_image .


    OPTIONAL {
        ?glycan ga:modified ?mod .
        ?mod ga:modified_glycan ?mod_glycan .
        ?mod_glycan ga:wurcs ?mod_wurcs .
        ?mod_glycan foaf:depiction ?mod_glycan_image .
        ?mod ga:method ?method .

        OPTIONAL {
            ?mod_glycan dcterms:identifier ?glytoucan .
        }

    }

    # abundance ratio
    OPTIONAL {
        ?gi exterms:percentage ?par .
        ?par ga:from ?par_from .
        ?par_from sio:has-value ?from .
        ?par ga:to ?par_to .
        ?par_to sio:has-value ?to .
    }

}
order by ?pos
```

## Output

```javascript
({
  json({result}) {


  var uniprotid = "";
  var sample = "";
  var sequence = "" ;
  var begin = "";
  var end = "";
  var wurcs = "";
  var glyimg = "";
  var glytoucanid = "";
  var from = "";
  var to = "" ;
  var features = [];
  var array = {};


  result.results.bindings.map((row) => {

        uniprotid = row.uniprotid.value ,
        sample = row.gsid.value ,
        sequence = row.seq.value ,
        begin = row.pos.value ,
        end = begin ,
        glytoucanid  = row.glytoucan.value ,
        wurcs = row.mod_wurcs.value ,
        glyimg = row.mod_glycan_image.value,
        from = row.from.value,
        to = row.to.value

        var data = {
              "type" : "Glycoform",
              "category" : "PTM",
              "begin": begin,
              "end" : end ,
              "from" : from,
              "to" : to ,
              "GlyTouCanID" : glytoucanid,
              "wurcs" : wurcs,
              "glycan_image" : glyimg
        };

        features.push(data);
  });

  array = {
        "id" : uniprotid ,
        "sample" : sample,
        "sequence" : sequence,
        "features" : features
  };

  return array;


  },
	text({array}) {
		return array.json("\n");
	}
})

```


